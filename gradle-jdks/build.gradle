apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.external-publish-gradle-plugin'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integTest
}

tasks.build.dependsOn tasks.integTest

dependencies {
    api project(':gradle-jdks-distributions')
    api project(':gradle-jdks-json')
    api project(':gradle-jdks-setup')

    implementation('com.palantir.baseline:gradle-baseline-java') {
        exclude group: 'com.diffplug.spotless', module: 'spotless-plugin-gradle'
    }
    implementation 'com.palantir.gradle.failure-reports:gradle-failure-reports-exceptions'
    implementation 'com.palantir.gradle.utils:lazily-configured-mapping'
    implementation 'com.google.guava:guava'
    implementation 'commons-io:commons-io'
    implementation 'org.apache.commons:commons-compress'
    implementation 'org.ow2.asm:asm'

    compileOnly 'com.palantir.gradle.auto-parallelizable:auto-parallelizable-annotations'
    compileOnly 'org.immutables:value::annotations'
    annotationProcessor 'com.palantir.gradle.auto-parallelizable:auto-parallelizable'
    annotationProcessor 'org.immutables:value'

    testImplementation gradleTestKit()
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
}

gradlePlugin {
    plugins {
        jdks {
            id = 'com.palantir.jdks'
            displayName = 'Palantir JDK Auto-provisioning Plugin'
            description = 'Auto-provisions specific versions of JDKs'
            implementationClass = 'com.palantir.gradle.jdks.JdksPlugin'
        }
        palantirCa {
            id = 'com.palantir.jdks.palantir-ca'
            displayName = 'Palantir CA for gradle-jdks'
            description = 'Includes the Palantir CA from the system truststore'
            implementationClass = 'com.palantir.gradle.jdks.PalantirCaPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-jdks'
    vcsUrl = 'https://github.com/palantir/gradle-jdks'
    description = 'Download specific JDKs versions automatically'
    tags = ['java', 'jdks']
}

tasks.withType(JavaCompile).configureEach {
    it.options.errorprone.disable 'JavaxInjectOnAbstractMethod'
    it.options.errorprone.disable 'StrictUnusedVariable'
}

tasks.register('copyGradleJdkJar', Copy) {
    dependsOn ':gradle-jdks-setup:fatJar'
    def sourceJar = "${project(':gradle-jdks-setup').projectDir}/build/libs/gradle-jdks-setup-all-${project.version}.jar"
    inputs.file sourceJar
    outputs.dir "${projectDir}/src/main/resources"
    from sourceJar
    into "${projectDir}/src/main/resources"
    rename { 'gradle-jdks-setup.jar' }
}

tasks.test {
    environment("PROJECT_VERSION", project.version)
    dependsOn copyGradleJdkJar
    systemProperty 'ignoreDeprecations', 'true'
}



tasks.build.dependsOn tasks.copyGradleJdkJar
