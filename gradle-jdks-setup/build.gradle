import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'org.unbroken-dome.test-sets'


testSets {
    integrationTest
}

dependencies {
    // Avoid adding large compile dependencies here, as they will also be added in the gradle-jdks-setup-all*.jar using the fatJar task
    implementation project(':gradle-jdks-setup-common')

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'commons-io:commons-io'

    integrationTestImplementation project(':gradle-jdks')
    integrationTestImplementation 'commons-io:commons-io'
    integrationTestImplementation 'com.google.guava:guava'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
    integrationTestImplementation 'org.assertj:assertj-core'
}

def gradleJdksFunctions = file('src/main/resources/templates/gradle-jdks-functions.sh')

tasks.register('renderGradleJdksSetupScript', Copy.class) {
    from 'src/main/resources/templates/gradle-jdks-setup.template.sh'
    into 'src/main/resources'
    filter(ReplaceTokens, tokens: [
        'INSERT_GRADLE_JDKS_FUNCTIONS': gradleJdksFunctions.text,
    ])
    rename { 'gradle-jdks-setup.sh' }
    inputs.file(gradleJdksFunctions)
}

tasks.register('renderInstallJdksSetupScript', Copy.class) {
    from '../gradle-jdks-excavator-configurations/src/main/resources/templates/install-jdks.template.sh'
    into '../gradle-jdks-excavator-configurations/src/main/resources'
    filter(ReplaceTokens, tokens: [
            'INSERT_GRADLE_JDKS_FUNCTIONS': gradleJdksFunctions.text,
    ])
    rename { 'install-jdks.sh' }
    inputs.file(gradleJdksFunctions)
}

tasks.register('renderJdksScripts') {
    dependsOn tasks.renderGradleJdksSetupScript, tasks.renderInstallJdksSetupScript
}


tasks.register('fatJar', Jar) {
    dependsOn tasks.renderJdksScripts, tasks.jar, ':gradle-jdks-setup-common:jar'
    manifest {
        attributes 'Main-Class': 'com.palantir.gradle.jdks.setup.GradleJdkInstallationSetup'
    }
    archiveBaseName = 'gradle-jdks-setup-all'
    duplicatesStrategy = DuplicatesStrategy.FAIL
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(JavaCompile) {
    options.errorprone.disable 'PreferSafeLoggableExceptions', 'StringSplitter'
}

tasks.integrationTest {
    environment("PROJECT_VERSION", project.version)
    dependsOn tasks.build
}

tasks.build.dependsOn tasks.renderJdksScripts, tasks.fatJar

// DO NOT UPDATE! This needs to be kept as low as possible, such that it is compatible with all the jdk versions the
// consumers would want to set up with the JDK auto-management workflow (used by ./gradle-jdks-setup.sh and GradleWrapperMain.java classes)
javaVersion {
    target = 11
    runtime = 11
}
